CREATE OR REPLACE TRIGGER PV_CARGOS_AFIN_TG
AFTER INSERT ON GE_CARGOS
REFERENCING OLD AS OLD NEW AS NEW
FOR EACH ROW
DECLARE
/*
<Documentación TipoDoc = "Trigger">
<Elemento Nombre = "VPV_CARGOS_AFIN_TG" Lenguaje="PL/SQL" Fecha="25-05-2005" Versión="1.0" Programador="Yuri Alvarez Ambiente="BD">
<Retorno>NA</Retorno>
<Descripción>Actualiza los valores tipo y Monto de Descuento.</Descripción>
</Elemento>
</Documentación>
*/


TN_INDDISP        GA_ABOCEL.IND_DISP%TYPE;
TN_ABONADO        GA_ABOCEL.NUM_ABONADO%TYPE;
TV_SERIE          GA_ABOCEL.NUM_SERIE%TYPE;
TN_CODARTICULO    AL_ARTICULOS.COD_ARTICULO%TYPE;
GN_ROWID 		  ROWID;
GN_CANTIDAD		  NUMBER;
CV_INDEQUI        GA_EQUIPABOSER.IND_EQUIACC%TYPE;
CV_INDPROCINT     GA_EQUIPABOSER.IND_PROCEQUI%TYPE;
CV_NOMPARAMIND	  CONSTANT GA_PARAMETROS_SISTEMA_VW.NOM_PARAMETRO%TYPE:='INDICADOR_EQUIPO';
CV_NOMPARAMINDINT CONSTANT GA_PARAMETROS_SISTEMA_VW.NOM_PARAMETRO%TYPE:='EQUIPO_INTERNO';
CV_MODULO         CONSTANT GED_PARAMETROS.COD_MODULO%TYPE  :='GA';
CV_NOMPARAM       CONSTANT GED_PARAMETROS.NOM_PARAMETRO%TYPE :='COD_SIMCARD_GSM';
CN_PRODUCTO       CONSTANT GED_PARAMETROS.COD_PRODUCTO%TYPE := 1;
TV_VALPARAM       GED_PARAMETROS.VAL_PARAMETRO%TYPE;
GN_OPERLOCAL	  GE_OPERADORA_SCL.COD_OPERADORA_SCL%TYPE;


TV_VALPARAM_ECU   GED_PARAMETROS.VAL_PARAMETRO%TYPE;
CV_NOMPARAM_ECU   CONSTANT GED_PARAMETROS.NOM_PARAMETRO%TYPE :='APLIC_CARGOS_INDEM';
CV_TRUE           GED_PARAMETROS.VAL_PARAMETRO%TYPE :='TRUE' ;


NO_ERROR EXCEPTION;

BEGIN

	TV_SERIE :=' ';
	TV_VALPARAM :=' ';
	GN_CANTIDAD:= 0;


	GN_OPERLOCAL:= GE_FN_OPERADORA_SCL();

	TV_VALPARAM_ECU:= GE_FN_DEVVALPARAM (CV_MODULO,CN_PRODUCTO,CV_NOMPARAM_ECU);
	IF  (TV_VALPARAM_ECU = CV_TRUE) THEN --HABILITADO PARA ECUADOR

			TV_VALPARAM := GE_FN_DEVVALPARAM (CV_MODULO,CN_PRODUCTO,CV_NOMPARAM);

			SELECT valor_texto
			  INTO CV_INDEQUI
			  FROM GA_PARAMETROS_SISTEMA_VW
			 WHERE nom_parametro = CV_NOMPARAMIND
			   AND cod_operadora = GN_OPERLOCAL;

			SELECT valor_texto
			  INTO CV_INDPROCINT
			  FROM GA_PARAMETROS_SISTEMA_VW
			 WHERE nom_parametro = CV_NOMPARAMINDINT
			   AND cod_operadora = GN_OPERLOCAL;

			 TN_ABONADO := :NEW.NUM_ABONADO;
			 TV_SERIE   := :NEW.NUM_SERIE;

		     IF (TN_ABONADO IS NOT NULL) AND (TN_ABONADO > 0) THEN
			 		BEGIN
						SELECT NVL(a.IND_DISP,1)
						INTO   TN_INDDISP
						FROM   GA_ABOCEL a
						WHERE  num_abonado = TN_ABONADO;
					EXCEPTION
							 WHEN NO_DATA_FOUND THEN
							 	  RAISE NO_ERROR;
					END;

				IF TN_INDDISP = 1 THEN
				   	BEGIN
						SELECT ROWID
						,a.cod_articulo
						INTO  GN_ROWID ,TN_CODARTICULO
						FROM  GA_EQUIPABOSER a
						WHERE a.num_abonado = TN_ABONADO
						AND   a.num_serie = TV_SERIE
						AND   a.ind_procequi = CV_INDPROCINT
						AND   a.imp_cargo IS NULL
						AND   a.tip_dto   IS NULL
						AND   a.Val_dto   IS NULL
						AND   a.fec_alta = (SELECT MAX(b.fec_alta)
						                    FROM   GA_EQUIPABOSER b
									   	 	WHERE  b.num_abonado = TN_ABONADO
											AND    b.num_serie = TV_SERIE );
					EXCEPTION
					WHEN NO_DATA_FOUND THEN
						 RAISE NO_ERROR;
					END;

					SELECT COUNT(1)
					INTO   GN_CANTIDAD
					FROM   al_articulos b
					WHERE  b.cod_articulo = TN_CODARTICULO
					AND    b.ind_equiacc =  CV_INDEQUI
					AND    NOT b.tip_terminal = TV_VALPARAM
					AND    b.cod_conceptoart = :NEW.cod_concepto;

					IF 	 GN_CANTIDAD > 0 THEN

					 	 UPDATE GA_EQUIPABOSER a
					     SET a.tip_dto = :NEW.TIP_DTO
						 ,a.val_dto    = :NEW.VAL_DTO
						 ,a.imp_cargo  = :NEW.IMP_CARGO
					     WHERE ROWID = GN_ROWID;

					END IF;
				END IF;
		     END IF;
	END IF;

EXCEPTION
WHEN NO_ERROR THEN
	 NULL;
WHEN OTHERS THEN
	 RAISE_APPLICATION_ERROR(-20099,'ERROR INESPERADO EN TRIGGER PV_CARGOS_AFIN_TG: ORA-'||TO_CHAR(SQLCODE),FALSE);
END;
/
SHOW ERRORS
